AC_INIT([lttngtop],[0.3],[jdesfossez@efficios.com])

AC_CONFIG_HEADERS([config.h])
AC_CONFIG_AUX_DIR([config])
AC_CONFIG_MACRO_DIR([m4])

AC_CANONICAL_TARGET
AC_CANONICAL_HOST

AM_INIT_AUTOMAKE([foreign dist-bzip2 no-dist-gzip])
AM_MAINTAINER_MODE([enable])

# Enable silent rules if available (Introduced in AM 1.11)
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)

# Checks for C compiler
AC_USE_SYSTEM_EXTENSIONS
AC_PROG_CC
AC_PROG_CC_STDC

# Checks for programs.
AC_PROG_MAKE_SET
AC_PROG_YACC
AC_PROG_LEX

LT_INIT

# Checks for typedefs, structures, and compiler characteristics.
AC_C_INLINE
AC_TYPE_PID_T
AC_TYPE_SIZE_T

AX_C___ATTRIBUTE__
AS_IF([test "x$ax_cv___attribute__" = "xyes"],
	[:],
	[AC_MSG_ERROR([The compiler does not support __attribute__ extensions])])

# Checks for library functions.
AC_FUNC_MALLOC
AC_FUNC_MMAP
AC_CHECK_FUNCS([bzero gettimeofday munmap strtoul])

# Check for libuuid
AC_CHECK_LIB([uuid], [uuid_generate], [],
	[AC_MSG_ERROR([Cannot find the libuuid library.])]
)

# Check for libpopt
AC_CHECK_LIB([popt], [poptGetContext], [],
        [AC_MSG_ERROR([Cannot find the popt library.])]
)

# Check for libncurses
AC_CHECK_LIB([ncurses], [initscr], [], [HAVE_NCURSES=0]
)

# Check for libpanel, also part of libncurses
# (but this check will add the required -lpanel flag)
AC_CHECK_LIB([panel], [update_panels], [], [HAVE_PANELS=0]
)

# Check for libbabeltrace
AC_CHECK_LIB([babeltrace], [bt_context_create], [],
	     [AC_MSG_ERROR([Cannot find the babeltrace library.])]
	     )

# Check for libbabeltrace-ctf
AC_CHECK_LIB([babeltrace-ctf], [bt_ctf_iter_create], [],
	     [AC_MSG_ERROR([Cannot find the babeltrace-ctf library.])]
	     )

# Check for Glib. It needs to be installed anyway or this macro will not be defined.
AM_PATH_GLIB_2_0([2.22.0], [],
	[AC_MSG_ERROR([Glib 2.22 is required in order to compile LTTngTop.
Please install the Glib development files.])], [gmodule]
)

pkg_modules="gmodule-2.0 >= 2.0.0"
PKG_CHECK_MODULES(GMODULE, [$pkg_modules])
AC_SUBST(PACKAGE_LIBS)
LIBS="$LIBS $GMODULE_LIBS"

DEFAULT_INCLUDES="-include config.h"
AC_SUBST(DEFAULT_INCLUDES)

PACKAGE_CFLAGS="$GMODULE_CFLAGS -Wall -Werror=format-security"
AC_SUBST(PACKAGE_CFLAGS)

AC_CONFIG_FILES([
	Makefile
	lib/Makefile
	src/Makefile
	src/kernel-ctl/Makefile
	doc/Makefile
	utils/Makefile
])
AC_OUTPUT

AS_IF([test "x$HAVE_NCURSES" = "x0"],[
       AS_ECHO()
       AS_ECHO_N("Ncurses GUI won't be compiled install ncurses if you want it.")
       ], [
	   CURSES_LIBS="-lcurses -lpanel"
	   AC_SUBST(CURSES_LIBS)
	   AS_ECHO()
	   AS_ECHO_N("Ncurses GUI will be compiled.")
	   ])
AS_ECHO()

